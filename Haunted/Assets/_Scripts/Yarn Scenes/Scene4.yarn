title: Scene4
tags:
---
<<NextPlayer ImageController "del24">>
<<NextBackground ImageController "none">>
<<NextNPC ImageController "none">>
<<PlayMusic SoundManager "scene4">>
Halloween...afternoon, really.

The home you grew up in is littered with moving boxes. It's been a while since you visited, and you realize you took the permanence of this place for granted. 

You're adrift at sea without a lifevest… metaphorically, of course. 
Part of you would prefer being lost to the sea over dealing with moving.

Moving houses is a pain under the best of circumstances. Not all moves necessitate hiding in the bathroom to dry your tears. 

There's a lot weighing on you now, especially…

->...your recent breakup.
    <<jump Scene4_Move_2a>>
->...your mom's move.
	<<jump Scene4_Move_2b>>
->...your uncertainty about life post-graduation.
    <<jump Scene4_Move_2c>>
===
title: Scene4_Move_2a
---
<<set $majorAnxiety to "relationship">>
The breakup is affecting you more than you'd like. 

<<if $porchTogether is true>>
    {$bff} was as amicable as can be. 
    Uncoupling as consciously as you could, the two of you didn't fight over anything.
    Except the vision.
<<else>>
    Admittedly, the circumstances were pretty bad.
    {$bff} drunk and screaming "We knew this would happen!" up at your window at 4am didn't look great, and it didn't feel great either.
<<endif>>

You knew all along it would be no good to force a relationship that wasn't meant to be.
Part of you wishes it could have been. 

You saw the end of this relationship before it even began, but you've never seen a new one start.
You're not fully convinced you'll ever find love again. 

Suddenly, it dawns on you that you've been sitting on a toilet with its lid down for way too long.

->Force yourself to stop thinking about this.
    <<jump Scene4_Move_3>>
===
title: Scene4_Move_2b
---
<<set $majorAnxiety to "family">>
The divorce was bad enough, spending all of high school shuttling back and forth between them.

Then Dad got his new job, and now he lives halfway across the country. You only even get to see him once or twice a year.

This house, this neighborhood, they were the stable place throughout it all. Mom was here, and your old room was here.
All your things. All your memories.

That other house...

And now Mom's downsizing to a condo across town, and you'll probably never come back here.

Suddenly, it dawns on you that you've been sitting on a toilet with its lid down for way too long.

->Force yourself to stop thinking about this.
    <<jump Scene4_Move_3>>
===
title: Scene4_Move_2c
---
<<set $majorAnxiety to "future">>
It feels like you blinked, and suddenly, you graduated. How could each individual semester feel so slow, while college overall passed by in a flash?

You didn't really make the most of it, academically or socially.

You're not a student anymore. 
You have to figure out your life now. If you don't, people will judge you. 
You're sure of it.

Suddenly, it dawns on you that you've been sitting on a toilet with its lid down for way too long.

->Force yourself to stop thinking about this.
    <<jump Scene4_Move_3>>
===
title: Scene4_Move_3
---
<<NextNPC ImageController "mom1">>
You force yourself to leave the privacy of the restroom. Mom is slumped on the couch, taking a break between flurries of stuffing boxes into the U-Haul.

“So, did you want to, uh…” You almost lose track of your train of thought. “Did you want to keep packing up?”

Your mom sighs grandly. "Let's call it for today."

“Thank fuck. I didn't wanna be the first to say it.”

Your mom laughs. "Since when do you get to use that kind of language in front of me?"

“I'm literally a college graduate, Mom. How old do I have to be to get f-bomb privileges?”

“Maybe when you're thirty… or whenever you become a mom." Mom leans back into the couch, closing her eyes. "Whichever comes first.”

->"Oh, thirty's definitely coming first."
    <<jump Scene4_Move_4a>>
->"Oh, I am definitely not having kids."
    <<jump Scene4_Move_4b>>
===
title: Scene4_Move_4a
---
<<if $majorAnxiety is "relationship">>
    “Maybe I'll have kids, with a major emphasis on maybe. But it won't be anytime soon! I don't even have a partner..."
<<else>>
    “Maybe I'll have kids, with a major emphasis on maybe. But it won't be anytime soon!”
<<endif>>

“Just make sure you plan it out," Mom murmurs, already half asleep. "You were planned, but I can't say the same for some of my friend's kids.”

<<if $majorAnxiety is "relationship">>
    But what does a plan help? You saw the plan for your relationship with {$bff} plenty of time in advance, and it ruined everything.
    <<if $porchTogether is false>>
        Now you're all alone. And {$bff} hates you.
    <<else>>
        Now you're all alone. And {$bff} doesn't love you anymore.
    <<endif>>
<<elseif $majorAnxiety is "family">>
    But what does a plan help? Your parents had a plan, and now they live on opposite sides of the country.
<<else>>
    A plan? You wish. If you had a post-graduation plan, you wouldn't be worrying so much right now.
<<endif>>

Your mom interrupts this spiraling train of thought with a loud yawn.

You can't resist the contagion.

->Yawn too.
    <<jump Scene4_Move_5>>
===
title: Scene4_Move_4b
---
<<set $parenthoodRejection to true>>

<<if $majorAnxiety is "family">>
    “Absolutely not. I am never having kids. And I mean never EVER.”
    You stop yourself before you say something unwise, like "I don't want to end up like you and Dad!"
<<else>>
    “Absolutely not. I am never having kids. And I mean never EVER.”
<<endif>>

“As long as you have a plan," Mom murmurs, already half asleep. "You were planned, but I can't say the same for some of my friend's kids.”

<<if $majorAnxiety is "relationship">>
    Sure, sure, "a plan". Where did that get you and {$bff}?
    <<if $porchTogether is false>>
        Screaming and crying and once you did actually throw up while fighting with them.
    <<else>>
        Sad and slowly falling apart, just the way the house warned you.
    <<endif>>
<<elseif $majorAnxiety is "family">>
    But what does a plan help? Your parents had a plan, and now they live on opposite sides of the country.
<<else>>
    A plan? You wish. If you had a post-graduation plan, you wouldn't be worrying so much right now.
<<endif>>

Your mom interrupts this spiraling train of thought with a loud yawn.

You can't resist the contagion.

->Yawn too.
    <<jump Scene4_Move_5>>
===
title: Scene4_Move_5
---
“Moving is exhausting,” your mom remarks, yawning again.

“You're telling me…” 

“I think I might take a nap here… I'll probably be an hour or so, okay?”

The possibility of a few minutes to yourself is both chilling and tempting.

“Okay. I might go take a walk," you say, reaching for your coat.

“Isn't it chilly out?”

“I'll be fine.” Even if it's too cold, you need some fresh air.

"Okay." Mom sprawls across the couch, and you wonder if she's relieved to have a moment of her own as well. "We'll get some food when you get back, okay?"

"Alright."

->Head out.
    <<jump Scene4_Walk_1>>
===
title: Scene4_Walk_1
---
<<NextNPC ImageController "none">>
<<NextBackground ImageController "neighborhood">>
You almost forgot it's Halloween. It's too early for trick-or-treaters. 
You wonder where all your werewolf costumes ended up. 

You wish you could go back to that carefree time. 
Hanging out with your friends, seeing what costumes they picked out, getting butterflies over {$bff}… it feels so long ago now. 
You miss when your biggest concern of the night was what candy to eat first.

But it wasn't really all rainbows and sunshine. 
<<if $majorAnxiety is "relationship">>
    Your childhood crush on {$bff} was easier than loving them became, but it was small and, you know now, doomed.
<<elseif $majorAnxiety is "family">>
    Your parents hadn't broken up yet, but that doesn't mean the cracks weren't already there.
<<else>>
    When you were a child, you didn't worry about the future, but it was still coming, relentless.
<<endif>>

You long to return to a time that never existed, never could exist. There's no point in this.

->Reorient yourself to the present.
    <<jump Scene4_Walk_3>>
===
title: Scene4_Walk_3
---
What are you experiencing right this moment? 

Well, for one, the cold is nipping at your skin. 
You're walking nowhere in particular. 
Where exactly did you intend on going?

You've always known.

->Head towards the house.
    <<jump Scene4_Walk_4>>
===
title: Scene4_Walk_4
---
The sun is sinking low, and the trick-or-treaters are emerging.

Today is a Saturday - a dream come true for any children getting into the Halloween spirit. 
You feel jealous, and then ridiculous for that jealousy.

A couple of androgynous teenagers across the road catch your eye. One has their arm around the other, and they're headed away from you. 

The taller of the two wears a werewolf costume, stirring up your nostalgia more than ever. It puts all your old costumes to shame.

Their partner wears a cat ear headband over a plain sheet with whiskers drawn on. They also wore cat paws and a tail. 
You're impressed at how they've combined two stereotypical Halloween costumes in a way you've never seen before.

->Compliment their costumes.
    <<jump Scene4_Walk_5a>>
->Keep walking.
    <<jump Scene4_Walk_5b>>
===
title: Scene4_Walk_5a
---
You yell across the street, “Nice costumes, you two!” 
They both turn their heads to look at you. You assume from their confused glances that they hadn't heard you. 
“Looking super cool!”

The werewolf gives you a dirty look. You can't tell what facial expression the ghost cat is making… probably for the best.

You're pretty sure you hear one say to the other, “Isn't she, like, 30?” 

Still in your early 20s and already being mistaken for 30. You're not sure how that makes you feel…

->Tell yourself you're still young.
    <<jump Scene4_Walk_6a>>
->Try to get used to the idea of getting older.
    <<jump Scene4_Walk_6b>>
===
title: Scene4_Walk_5b
---
You decide not to bother the couple.

You don't want to be the awkward adult commenting on their costumes. 

Watching them go makes you miss your werewolf costume more than ever.

You miss {$bff} too.

<<if $bff is "Ash">>
    But even if the white sheet haphazardly draped atop their head reminds you of them, it's not them.
<<elseif $bff is "Jules">>
    But even if the adorable cat accessories remind you of them, it's not them.
<<else>>
    But even if the sheer weirdness of their costume combination reminds you of them, it's not them. 
<<endif>>

Part of you wonders if you'll ever find love again.

->Of course you'll find love again.
    <<jump Scene4_Walk_6c>>
->Well, no shame in being single…
    <<jump Scene4_Walk_6d>>
===
title: Scene4_Walk_6a
---
<<set $agingAnxiety to true>>

Sure, you're an adult who graduated college, but you're still young. You're not going to be 30 anytime soon. 
And, really, anything's better than being a teenager again.

There's nothing to fear about the future...not when the house will tell you what comes next.

->Walk the rest of the way to the haunted house.
    <<jump Scene4_House_1>>
===
title: Scene4_Walk_6b
---
<<set $agingAnxiety to false>>

Getting used to the idea of aging is easier said than done. 
But there's no way around it. 

Except dying young, which you'd obviously like to avoid.

You may not be 30 now, but eventually, you will be.
And maybe...maybe that's what the house will show you tonight.

->Walk the rest of the way to the haunted house.
    <<jump Scene4_House_1>>
===
title: Scene4_Walk_6c
---
<<set $relationshipConfidence to true>>

Of course you'll find love again someday.
There's a whole world outside the people you knew in elementary school, high school, college…

So many new people you might come to know.
Maybe your next partner will be {$bff}'s total opposite. 

<<if $bff is "Ash">>
    Someone sincere, heartfelt. They could go a single sentence without dripping sarcasm.
<<elseif $bff is "River">>
    Someone approachable, easy to understand. With that person, what you see would be what you get.
<<else>>
    Someone tough, a bit of a rebel. That person would definitely be no pushover.
<<endif>>

Maybe theirs will be the face you find in the mirror tonight, at the house.

->Walk the rest of the way to the haunted house.
    <<jump Scene4_House_1>>
===
title: Scene4_Walk_6d
---
<<set $relationshipConfidence to false>>

It's hard to imagine dating anyone besides {$bff}. Stupid as that feels, they were your first, and as of now, only love. 

Maybe it was silly to think your high school sweetheart would be your soulmate too.

<<if $bff is "Ash">>
    <<if $porchTogether is false>>
        It's not like Ash made any secret of how dumb they thought the idea of soulmates was.
        Maybe that should have been a red flag...
    <<else>>
        Ash humored you when you said things like that, but you always knew they didn't believe it like you did.
        Part of you is still angry that they lied to you this way.
    <<endif>>
<<elseif $bff is "River">>
    <<if $porchTogether is false>>
        When River's parents detected an "aura of incompatibility" about you, River never really looked at you the same way.
        You brushed it off, but it certainly felt bad, being magically diagnosed as not good enough for your partner.
    <<else>>
        River tried to explain to you more than once that the soul is mutable, changeable, and that whoever suits your soul at one time may not at another.
        Maybe you should have taken those lectures to heart.
    <<endif>>
<<else>>
    <<if $porchTogether is false>>
        The more your relationship frayed, the more you insisted on it, and the further away Jules became.
        Like a cat retreating from a tail-pulling toddler.
    <<else>>
        You know Jules believed it too, at first.
        But in time, the certainty in their eyes faded, and everything became work, until it became nothing.
    <<endif>>
<<endif>>

But there's no point in thinking about them now. 
Even if you wish a little to see them in whatever vision the house provides you.

->Walk the rest of the way to the house.
    <<jump Scene4_House_1>>
===
title: Scene4_House_1
---
<<NextBackground ImageController "housefar">>
Here it is again… an eerily familiar sight. Part of you thought you'd never come back. 

The other part of you knew you were always destined to return to this house.

Something has changed in the past five years, but you can't put your finger on it. You thought growing up would mean you'd be less scared, but you still find yourself shivering. 

Creeped out or not, you refuse to be stopped. You're not a child anymore, after all. 

<<if $agingAnxiety is false>>
    No, you're an adult now… and that's okay.
<<elseif $agingAnxiety is true>>
    No, you haven't been one in several years now… how bizarre.
<<endif>>

->Go inside.
    <<jump Scene4_House_2>>
===
title: Scene4_House_2
---
<<NextBackground ImageController "mirror">>
In the spirit of not being a frightened kid anymore, you resolve to try something new. That's right - you're gonna go to the attic! 

Every time you came here before, you were way too scared to go up there.

The stairs creak with each step. The noise feels like it gets unnervingly louder as you ascend. 
You worry the old wood will cave through. 

Nonetheless, you keep walking. Child you would be so proud… and terrified.

->Climb the rest of the stairs.
    <<jump Scene4_House_3>>
===
title: Scene4_House_3
---
<<NextBackground ImageController "attic">>
You don't realize you're holding your breath until you climb the last stair and exhale. Like the house you came from, there are boxes strewn about the attic. 

Your initial browsing produces nothing but boxes littered across the floor, alongside a generous helping of dust bunnies. 

Sitting atop one of the boxes, however, is a distinctly un-boxlike shape.

You reach towards this not-box.

->Pick up the mysterious object.
    <<jump Scene4_House_4>>
===
title: Scene4_House_4
---
It's soft… soft in a very particular way. Your heart races at the oddly familiar texture. 

It's too dark to see much of anything, so you step towards the light of the window. 

Upon holding the mysterious soft object closer to you…

…your heart skips a beat. Del the dolphin! 

How did your childhood plushie end up here? You never took Del here, did you?

Countless memories come rushing back to you. In moments of uncertainty, this dolphin was your confidant. 

It's likely Del the dolphin knows at least one or two of your secrets no one else has heard. 

Did you bring Del here sometime and forget? Did someone else bring her here? Who would do that? 

Would {$bff} do such a thing?

<<if $bff is "Jules">>
    No. They were far too kind to pull a prank like that on you.
<<elseif $bff is "Ash">>
    No. It's not like they were above messing with you, but you're not sure this was their style. 
<<else>>
    No. As weird as they are, this is an entirely different brand of weird.
<<endif>>

There's a chance it's the same brand of dolphin. As special as Del may have been to you, you doubt the plushie your parents gifted you all those years back was one-of-a-kind. 

That explanation makes perfect sense… 

So why don't you believe it?

->Hold the dolphin plushie closer.
    <<jump Scene4_House_5a>>
->Put down the dolphin plushie.
    <<jump Scene4_House_5b>>
===
title: Scene4_House_5a
---
<<set $plushieRecognition to true>>

Something possesses you - not literally, you're pretty sure - to clutch the dolphin plushie closer to your chest. 

Perhaps most mystifyingly, you press your nose against the soft fabric and take in a whiff.

Scent is the sense strongest associated with memory. Not the first time facts from Trivia Night have guided your decisions. 

When you get a noseful of dust and begin coughing, you start to doubt your own logic.

This isn't just some random dolphin plushie. No… this is Del. 

Weird as it was for child you to name the dolphin after yourself.

Is this evidence that kid you was self-centered? Or horrifically uncreative? Or both?

Eventually, you put Del back where you found her. You could torture yourself all night wondering how she ended up there, but you have more important things to do.

<<if $majorAnxiety is "family">>
    Mom's probably getting hungry, wondering where you are...
<<elseif $majorAnxiety is "relationship">>
    <<if $relationshipConfidence is false>>
        The longer you linger here, the lonelier you'll get.
    <<else>>
        You're definitely not getting a jumpstart on new relationships in this nasty old place.
    <<endif>>
<<else>>
    <<if $agingAnxiety is true>>
        Every minute you waste feels like the future slipping away.
    <<else>>
        There's so much more to do in your life, and hiding out in a creepy old house probably doesn't need to be a priority.
    <<endif>>
<<endif>>

And without a vision, what was the point in coming here?

Still, you stop to gaze out the attic window of the house.

This place almost feels like a second home from your dreams… or your nightmares.

->Look out the window.
    <<jump Scene4_House_6>>
===
title: Scene4_House_5b
---
<<set $plushieRecognition to false>>

There's no use overthinking this dolphin plushie. Anything in this attic is surely haunted as the house containing it. 

Whether or not it's Del the dolphin, you're pretty sure that plushie is a cursed object now.

When you catch yourself shivering again, you tell yourself it's because of the cold. You're definitely not scared or anything.

You step towards the window, the last light of a beautiful sunset falling through it. 

Not that you stepped out of the dark because you were afraid… of course not…

You didn't set down the plushie because of fear. 

Okay, maybe it's a little motivated by fear. 

More importantly, however, you want to keep yourself in the present.

You could spend hours reminiscing on memories of a single plushie alone, but you have more important things to do.

<<if $majorAnxiety is "family">>
    Mom's probably getting hungry, wondering where you are...
<<elseif $majorAnxiety is "relationship">>
    <<if $relationshipConfidence is false>>
        The longer you linger here, the lonelier you'll get.
    <<else>>
        You're definitely not getting a jumpstart on new relationships in this nasty old place.
    <<endif>>
<<else>>
    <<if $agingAnxiety is true>>
        Every minute you waste feels like the future slipping away.
    <<else>>
        There's so much more to do in your life, and hiding out in a creepy old house probably doesn't need to be a priority.
    <<endif>>
<<endif>>

And without a vision, what was the point in coming here?

Still, you stop to gaze out the attic window of the house.

This place almost feels like a second home from your dreams… or your nightmares.

->Look out the window.
    <<jump Scene4_House_6>>
===
title: Scene4_House_6
---
//screen change here
You gaze into the distance, taking in the sight of your childhood neighborhood beneath a setting sun. It overwhelms you with emotions you struggle to describe. 
Maybe there's a pithy word for what you're feeling in another language, but all you know is English, and you're at a loss.

You pull yourself back, and instead of taking in an entire community, you focus solely on your own faintly mirrored reflection.

Your reflection in the window starts to ripple. It shows a picture much clearer than any ordinary window would.

<<NextBackground ImageController "none">>

The image blurs and distorts, arranging itself into a crystal clear view of an old lady on a rocking chair. 

“I wanted to see MY future, not some random old lady. What is that, my grandma?” 

All of your grandparents died before you were born. You haven't seen enough pictures to recognize them right away, but you're sure this woman isn't one of them.

Suddenly, you realize…

It's you.

->It's… me?
    <<jump Scene4_House_8>>
===
title: Scene4_House_8
---
That old lady is YOU. Not some random lady. Not your grandma.

Sure, it's several decades down the road, but there's no denying it. 

You're probably in your sixties here, but there's still a lively spark in your eyes. 

Old you would never be boring, after all.

<<if $agingAnxiety is false>>
    If you have to grow old someday, this isn't the worst way it could happen.
<<else>>
    You don't like it one bit. You're not supposed to be that old. It doesn't feel right.
<<endif>>

<<jump Scene4_House_9>>
===
title: Scene4_House_9
---
You also can't help but notice that you're alone.

<<if $relationshipConfidence is false>>
    It's hard to pretend like it doesn't feel like a kick in the chest.
    Even if you stayed single until the grave, hopefully you had other people to fill the void.
<<else>>
    You hope the love of your life is right nearby, whoever they are.
    Considering all the possibilities is more of an emotional rollercoaster than you're prepared for.
<<endif>>

<<jump Scene4_House_10>>
===
title: Scene4_House_10
---
“Is this really it?” you ask to no one in particular.

“I kinda wanted to see more than Little Old Lady Me sitting on a rocking chair…”

The image stays on older Delphine rocking back and forth, and slowly fades. 

But before she goes, you'd swear older you shakes her head at you, laughing as she disappears.
Does she know you wanted more? Is she mocking you for expecting anything else? 

Are you losing your mind? 

Probably. After all, you're staring intently at your own barely-there reflection in the window. 
<<NextBackground ImageController "attic">>

The image of you in your twilight years is burned into your brain.
But only your present self is visible now. 

This is the most alone you've felt in this house.

<<if $plushieRecognition is false>>
    In this lonely, cramped attic, there's only you and the dolphin plushie and the emptiness.
<<elseif $plushieRecognition is true>>
    In this lonely, cramped attic, there's only you and a dolphin plushie that shares a piece of your soul, as well as your name.
<<endif>>

<<jump Scene4_End>>
===
title: Scene4_End
---
->Go on.
    <<jump Scene5>>
===