title: Scene1
tags:
---
<<NextPlayer ImageController "del8">>
<<NextBackground ImageController "none">>
<<NextNPC ImageController "none">>
<<PlayMusic SoundManager "scene1">>
Halloween night. This year finally trick-or-treating with no parents hanging on, holding you back.

They finally caved, on the costume, on going out without them, on everything.

For an 8-year-old, you're a pretty forceful negotiator.

Before letting you out, Mom and Dad had just one condition,

(and when they actually agree on something, you know they're serious about it),

"Don't go near that nasty old place at the end of the street."

->"That nasty old place at the end of the street."
    <<jump Scene1_2>>
===

title: Scene1_2
---
<<NextBackground ImageController "housefar">>
Up on the hill, looking down upon the neighborhood with its empty-eyed broken window stare. 

The front yard is heaped with rotting old shingles, slipped loose from the roof like the baby teeth you started losing last year.

A nasty old place.

<<NextBackground ImageController "none">>
"Why any of you kids would want to go up there is beyond me," says Mom. "But every year someone always does."

"And then they get hurt," adds Dad. "Delphine, do you remember what I told you about tetanus?"

->"Yes. You get it from rusty nails and nasty old houses."
    <<jump Scene1_3>>
->"No. It was boring."
    <<jump Scene1_3>>
===

title: Scene1_3
---
"It's a terrible disease and you get it from old buildings, so don't go up there!" Dad pats you on the shoulder. "You're all we've got, kiddo. No back-ups! Be safe."

And finally you're free. 
<<NextBackground ImageController "neighborhood1">>

You light out for the meet-up spot, werewolf tail streaming behind you.

Mom took you to three Halloween stores before you found the tail and ears that matched to your satisfaction.

Under the cool white moon of the streetlight, your three friends await --

<<NextNPC ImageController "ash8">>
Ash - managing to look bored even under the white sheet of a no-effort ghost costume.

<<NextNPC ImageController "jul8">>
Jules - cat ears, face paint, leotard, and tail all add up to the kind of adorable costume parents love for pictures.

<<NextNPC ImageController "riv8">>
River - dressed as…no, you really can't say what the costume is meant to be.

<<NextNPC ImageController "none">>
The four of you, inseparable since kindergarten, best friends.

Though lately the thought has crossed your mind -- which of them is your best-best friend…?

->Maybe it's Ash…Ash is funniest…
    <<jump Scene1_4_Ash>>
->Maybe it's Jules…Jules is nicest…
    <<jump Scene1_4_Jules>>
->Maybe it's River…River is…different.
    <<jump Scene1_4_River>>
===

title: Scene1_4_Ash
---
And it's Ash who sees you first, poking one arm free to wave.

//<<NextNPC ImageController "jul8">>
Jules hops up and down in delight. "Delphine's here!"

<<jump Scene1_5>>
===

title: Scene1_4_Jules
---
//<<NextNPC ImageController "jul8">>
Jules sees you and bounds to the edge of the streetlight's pool of illumination. "Delphine! You're a werewolf!"

<<jump Scene1_5>>
===

title: Scene1_4_River
---
//<<NextNPC ImageController "riv8">>
River sees you, blinks at you twice, smiling a squinty nervous smile.

Ash and Jules don't realize you've arrived until you join them under the streetlight.

<<jump Scene1_5>>
===

title: Scene1_5
---
<<NextNPC ImageController "ash8">>
"Okay, now we're all here, can we pleeeease get some candy?" says Ash. "It's literally Halloween and I haven't had a single candy yet!"

<<NextNPC ImageController "jul8">>
"Where should we start?" asks Jules. "I heard the Lawsons are giving out full-size candy bars!"

<<NextNPC ImageController "riv8">>
"Chocolate makes my stomach hurt," says River. "Can't we start at Mark's? His parents had Jolly Ranchers last year."

->Side with Jules.
    <<jump Scene1_6_Jules>>
->Side with River.
    <<jump Scene1_6_River>>
->See what Ash thinks.
    <<jump Scene1_6_Ash>>
===

title: Scene1_6_Ash
---
<<NextNPC ImageController "ash8">>
"I don't care where we start, as long as we frickin' start already! Let's go, or everybody else is gonna get the good candy." 

You're amazed Ash's eyeroll is visible through the ghost costume's tiny eye slits.

->Side with Jules.
    <<jump Scene1_6_Jules>>
->Side with River.
    <<jump Scene1_6_River>>
===

title: Scene1_6_Jules
---
<<NextNPC ImageController "jul8">>
"I want the full-size candy bars!" you declare.

Jules squeals happily, and the party sets off.

To the Lawsons!

<<set $jollyRanchersChosen to false>>

<<jump Scene1_7>>
===

title: Scene1_6_River
---
<<NextNPC ImageController "riv8">>
"I want some Jolly Ranchers!" you declare.

Candy bucket held high, River cheers your decision.

To Mark's house!

<<set $jollyRanchersChosen to true>>

<<jump Scene1_7>>
===

title: Scene1_7
---
<<NextNPC ImageController "none">>
<<if $jollyRanchersChosen is true>>
    <<NextBackground ImageController "neighborhood2">>
    Mark's parents are dressed up as witches, and they offer heaping handfuls of Jolly Ranchers from within their arcane cauldron. 

    Everyone's candy bucket gains a satisfying heft.
<<else>>
    <<NextBackground ImageController "neighborhood3">>
    The rumors are true -- full-size candy bars at the Lawsons! 

    And in their unbelievable goodness, they offer River a glow-in-the-dark rubber ball printed to look like an eyeball. 

    Predictably, this delights River more than candy ever would.
<<endif>>

The first house of the night and already a windfall.

Halloween is so much better post-parents.

As the four of you wander down the street, it catches your eye.

<<NextBackground ImageController "housefar">>
Up on the hill, half-invisible against a black sky.

The old house.

->Ask the others about the house.
    <<jump Scene1_8a>>
->Keep quiet.
    <<jump Scene1_8b>>
===

title: Scene1_8a
---
"Hey, did your parents say anything about that old house?" you ask.

Jules shivers. 

Ash slumps. 

River looks away.

<<NextNPC ImageController "jul8">>
"They said to stay out."

<<NextNPC ImageController "ash8">>
"It's too dangerous."

<<NextNPC ImageController "riv8">>
"And it's gross."

->Point to the haunted house. "We should go up there!"
    <<jump Scene1_9>>
===

title: Scene1_8b
---
<<NextNPC ImageController "ash8">>
"River," and there's something ominous in Ash's tone. "What are you supposed to be?"

There are not a lot of clues, it's true. 

Tin foil, cardboard, glue, googly eyes, and construction paper have come together as something unclear in both form and meaning.

<<NextNPC ImageController "riv8">>
"Oh, I'm a ghost from outer space."

<<NextNPC ImageController "jul8">>
"You're the ghost…of an alien?" asks Jules.

<<NextNPC ImageController "riv8">>
"Hmm, I never really thought about it like that."

<<NextNPC ImageController "ash8">>
"What did you think about it like then?" Ash says.

->Point to the haunted house. "We should go up there!"
    <<jump Scene1_9>>
===

title: Scene1_9
---
<<NextNPC ImageController "none">>
"We should go up there!" The more you say it, the more real it is.

There's nothing stopping you. Not anymore.

Your friends look on, stricken.

->Appeal to River.
    <<jump Scene1_10_River>>
->Appeal to Jules.
    <<jump Scene1_10_Jules>>
->Appeal to Ash.
    <<NextNPC ImageController "ash8">>
    <<jump Scene1_10_Ash>>
===

title: Scene1_10_Ash
---
<<NextNPC ImageController "ash8">>
"Ash, come on!" You pull on Ash's sheet until it threatens to slip loose. "It'll be super scary!"

"No thanks." Ash yanks back possession of the sheet. "It'll be lame. It's just a trashed old house."

<<jump Scene1_11>>
===

title: Scene1_10_River
---
<<NextNPC ImageController "riv8">>
"River, come on!" you say, tugging on one of River's rubbery green gloves. "It'll be so cool."

<<if $jollyRanchersChosen is true>>
    "Nuh uh." River pulls away. "My parents say the vibrations are completely off there. It's a negative energy confluence."
<<else>>
    "No way!" River pulls away. "I still want to get some Jolly Ranchers, not visit a negative energy nexus close-up!"
<<endif>>

<<jump Scene1_11>>
===

title: Scene1_10_Jules
---
<<NextNPC ImageController "jul8">>
"Jules, come on!" You gesture to Jules with one furry werewolf paw. "It'll be fun."

<<if $jollyRanchersChosen is false>>
    "Sorry, Delphine." Jules clutches the costume cat's tail like a weapon, or a security blanket. "My mom said it's dangerous to go inside."
<<else>>
    "I still wanna get some candy bars!" Jules retreats behind Ash, peering at you with a hurt expression. "Not go into that creepy house!"
<<endif>>

<<jump Scene1_11>>
===

title: Scene1_11
---
<<NextNPC ImageController "none">>
Unbelievable. What a bunch of nerds.

"Fine! I'll go by myself 'cause you're all a bunch of scaredy cats!"

You pull away from the group, storming up the front walk of the old house.

As you go, you hear them call out. "Delphine! Delphine, wait!"

<<jump House1_1>>
===

title: House1_1
---
<<NextBackground ImageController "porch">>
Each step you take on the house's porch is like the next movement in a vast symphony of creaks and groans.
<<PlaySound SoundManager "floorcreak">>

There's something unstable in the warp of the floor beneath you, like walking across waves.

When you look back, you see your friends nervously clustered at the bottom of the front walk.

If you turn back now, you'll be a scaredy cat.

//these could be clicks
->Look in the front window.
    <<jump House1_2a>>
->Knock on the front door.
    <<jump House1_2b>>
===

title: House1_2a
---
You peer through the hazy glass and see nothing.

The darkness is absolute in its severity. Except-

A silver glint, light from the street below catching something just inside the doorway.

//this could be a click
->Knock on the front door.
    <<jump House1_2b>>
===

title: House1_2b
---
You approach the front door.

It's not at all like the familiar front door at home, always hosting a seasonal wreath and a cheerful welcome mat before it.

This door is wide, its faded white paint chipped away to reveal the shards of dark wood beneath.

Far above your head, a tarnished door knocker in the shape of an hourglass, the ring pierced through its neck.

//this could be a click
->Knock.
    <<jump House1_3>>
===

title: House1_3
---
You stretch as far as you can, taking hold of the door knocker with your fingertips.

You knock twice, the sound resonating through the old door.

<<PlaySound SoundManager "doorcreak">>
With a creak, it falls open, wide enough for you to enter.

->Go on.
    <<jump House1_4>>
===

title: House1_4
---
<<NextBackground ImageController "mirror">>
Inside the house is warmer than you expected, and more humid.

The entryway is buried in drifts of trash, dust, and splintered wood.

There is one piece of intact furniture.

Across from the doorway sits a side table with a dusty plastic bowl on it and a silver mirror hanging above it.

There's something in the bowl.

->Look inside.
    <<jump House1_5>>
===

title: House1_5
---
<<NextBackground ImageController "bowl">>
In the bowl is one solitary piece of ancient, dusty candy.

Red and yellow wrapper, faded and wrinkled with age.

Proof of your escapade.

(Obviously you wouldn't eat it. Probably has tetanus.)

//this could be a click
->Take the candy.
    <<jump House1_6>>
===

title: House1_6
---
You grab the candy, grateful your werewolf gloves protect your hands from the puddle of dust around it.

As you toss the candy into your bucket, you glance up, into the mirror, and see-
<<NextBackground ImageController "vision1">>
->Yourself?
    <<jump House1_7>>
===

title: House1_7
---
It must be you standing there, in the entryway of the house.

But you're different, older, taller.

Sad.

And there's someone else there.

->Ash?
    <<jump House1_8_Ash>>
->River?
    <<jump House1_8_River>>
->Jules?
    <<jump House1_8_Jules>>
===

title: House1_8_Ash
---
<<NextNPC ImageController "ash40">>
It could be Ash, face half-obscured in shadow.

<<set $bff = "Ash">>
<<if $jollyRanchersChosen is true>>
    <<set $moves to "Jules">>
    <<set $pal to "River">>
<<else>>
    <<set $moves to "River">>
    <<set $pal to "Jules">>
<<endif>>

->Turn around.
    <<jump House1_9>>
===

title: House1_8_River
---
<<NextNPC ImageController "riv40">>
It could be River, eyes round and fearful.

<<set $bff = "River">>
<<if $jollyRanchersChosen is true>>
    <<set $moves to "Ash">>
    <<set $pal to "Jules">>
<<else>>
    <<set $moves to "Jules">>
    <<set $pal to "Ash">>
<<endif>>

->Turn around.
    <<jump House1_9>>
===

title: House1_8_Jules
---
<<NextNPC ImageController "jul40">>
It could be Jules, looking nervously back at the open door.

<<set $bff = "Jules">>
<<if $jollyRanchersChosen is true>>
    <<set $moves to "Ash">>
    <<set $pal to "River">>
<<else>>
    <<set $moves to "River">>
    <<set $pal to "Ash">>
<<endif>>

->Turn around.
    <<jump House1_9>>
===

title: House1_9
---
<<NextNPC ImageController "none">>
<<NextBackground ImageController "mirror">>
There's no one there.

But the house sighs around you, a creak and a rush of air, as if someone was there only a moment ago.

You feel suddenly, terribly nauseous.

You flee the house, tearing across the porch, down the steps, and back to the sidewalk.
<<NextBackground ImageController "housefar">>

Your friends cluster around you, all asking about what happened.

->"I saw somebody in the house!"
    <<jump After1_1a>>
->"Nothing!"
    <<jump After1_1b>>
===

title: After1_1a
---
"In the mirror, I saw somebody following me into the house! It was you!"

<<if $bff is "Ash">>
    You point to Ash.
<<elseif $bff is "Jules">>
    You point to Jules.
<<else>>
    You point to River.
<<endif>>

<<NextNPC ImageController "ash8">>
"We've been standing here the whole time," says Ash. "Nobody followed you."

<<NextNPC ImageController "riv8">>
River nods vigorously.

<<NextNPC ImageController "jul8">>
"Are you sure you feel okay?" asks Jules. "Maybe we should head home."

<<NextNPC ImageController "ash8">>
"Who cares if you saw somebody in there?" Ash whines. "We've only been to a few houses, and it's almost curfew! Come on!"

->"It really happened!"
    Ash ignores you, leading the way down the street, back into the neighborhood.
    <<jump After1_2>>
->"Fine."
    You lead your friends down the street, back into the neighborhood.
    <<jump After1_2>>
===

title: After1_1b
---
"I found this nasty old candy in there, and that's all."

You show them your prize.

<<NextNPC ImageController "riv8">>
"Ooh, Bit-O-Honey!" says River, reaching out.

You snatch your hand away. "Don't eat that! It's ancient."

<<if $jollyRanchersChosen is true>>
    <<NextNPC ImageController "jul8">>
    "Yeah, you can have my Jolly Ranchers if you want, just don't eat haunted candy!" Jules adds.
<<else>>
    <<NextNPC ImageController "jul8">>
    "Why don't we go to Mark's house next?" suggests Jules. "We'll get you some Jolly Ranchers."
<<endif>>

<<NextNPC ImageController "ash8">>
"Let's get out of here." Ash starts walking, not waiting for the rest of you. "We've only been to a few houses, and it's almost curfew."

->Leave the house behind.
    <<jump After1_2>>
===

title: After1_2
---
<<NextBackground ImageController "none">>
<<NextNPC ImageController "none">>
It's easy enough to leave the house behind, to distract yourself with trick-or-treating.

Your friends forget your bravery and your flight soon enough, and it's back to nothing but talk of candy and costumes.

You half-listen, play along.

At home after curfew, sitting on your bed, you ponder the wizened old candy bar.

If it wasn't for this little blob of condensed sugar, you might not believe it was real.

But you know what you saw.

That night you dream about the house.

<<NextBackground ImageController "housefar">>
You're there again, and you're not alone.

Every time you look for the other person, they slip away.

You wake up crying.

<<NextBackground ImageController "none">>
The next morning, Mom sees your sleepless eyes and delivers a blanket ban on too much sugar right before bed.

All your Halloween spoils go up on top of the fridge to be doled out at parental discretion.

All except the ancient Bit-O-Honey, hidden in the back of your sock drawer.

<<jump Scene1_End>>
===

title: Scene1_End
---
->Go on.
    <<jump Scene2>>
===